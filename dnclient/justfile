name := "dnclient"
base_images := "
quay.io/fedora-ostree-desktops/base-atomic:41 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:42 x86_64,aarch64
quay.io/fedora-ostree-desktops/base-atomic:43 x86_64,aarch64
"

import '../sysext.just'

all: default

# Custom download step to get the binary
download-manual arch=arch:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    mkdir binaries
    cd binaries

    version=$(curl -s --location --fail https://api.defined.net/v1/downloads | jq --raw-output '.data.versionInfo.latest.dnclient')

    echo "⬇️ Downloading dnclient v${version}"

    if [[ "{{arch}}" == "x86_64" ]]; then
        arch=amd64
    else
        arch=arm64
    fi

    download_link=$(curl -s --location --fail https://api.defined.net/v1/downloads | jq --raw-output --arg latest_version ${version} --arg arch "linux-${arch}" '.data.dnclient.[$latest_version].[$arch]')
    sha=$(curl -s --location "$download_link.sha256")
    curl -s --location --fail --output "dnclient" "$download_link"

    echo "$sha dnclient" | sha256sum --check
    
    echo "${version}" > ../version
    sha256sum dnclient >> ../inputs

version:
    true

install-manual:
    #!/bin/bash
    set -euo pipefail
    if [[ -n "{{debug}}" ]]; then
      set -x
    fi

    if [[ "${UID}" == "0" ]]; then
        SUDO=""
    else
        SUDO="sudo"
    fi

    cd rootfs

    echo "➡️ Installing dnclient"
    ${SUDO} install -D -m 755 ../binaries/dnclient usr/bin/dnclient

    ${SUDO} chown -R root: usr
